IF
	AttackedBy([GOODCUTOFF], Default)
      Allegiance(Myself, NEUTRAL)
      See(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		Enemy()
END

IF
	AttackedBy([GOODCUTOFF], Default)
      !See(LastAttackerOf(Myself))
      Global("BW05_NotInSight","LOCALS",0)
THEN
	RESPONSE #100
            SetGlobal("BW05_NotInSight","LOCALS",1)
            Enemy()
END

IF
      See(LastAttackerOf(Myself))
      Global("BW05_NotInSight","LOCALS",1)
THEN
	RESPONSE #100
            SetGlobal("BW05_NotInSight","LOCALS",0)
END

IF
      !See(LastAttackerOf(Myself))
      Global("BW05_NotInSight","LOCALS",1)
THEN
	RESPONSE #100
            AttackReEvaluate(LastAttackerOf(Myself),30)
END

IF
	AttackedBy([ANYONE], Default)
      !See(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		ForceSpell(LastAttackerOf(Myself),WIZARD_FLAME_ARROW)
END

IF
	Global("BW05_GraTalkedToTergius","GLOBAL",3)
      See([ENEMY.0.0])
THEN
	RESPONSE #100
		SetGlobal("BW05_GraTalkedToTergius","GLOBAL",4)
            ApplySpell(Myself,CLERIC_CONJURE_EARTH_ELEMENTAL)
            ApplySpell(Myself, WIZARD_MIRROR_IMAGE)
            ApplySpell(Myself, WIZARD_GHOST_ARMOR)
            ApplySpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
            ApplySpell(Myself,WIZARD_ABSOLUTE_IMMUNITY)
            ForceSpell(Myself,RED_FIREBALL)
END

IF
	!Dead("BW05TALI")
      HPLT("BW05TALI",70)
THEN
	RESPONSE #100
		ReallyForceSpell("BW05TALI",CLERIC_CURE_CRITICAL_WOUNDS)
END

IF
	HPLT(Myself,60)
THEN
	RESPONSE #100
		ApplySpell(Myself,CLERIC_CONJURE_EARTH_ELEMENTAL)
            ApplySpell(Myself, WIZARD_IMPROVED_INVISIBILITY)
            ApplySpell(Myself, WIZARD_MIRROR_IMAGE)
            ForceSpell(Myself,CLERIC_CURE_CRITICAL_WOUNDS)
            ForceSpell(Myself,CLERIC_CURE_CRITICAL_WOUNDS)
END

IF
	!Global("BW05_GraTalkedToTergius","GLOBAL",3)
      See([ENEMY.0.0])
	CheckStatLT(LastSeenBy(Myself), 56, RESISTMAGIC)
      !Allegiance(Myself, ENEMY)
      !Class(LastSeenBy(Myself),INNOCENT)
	!Class(LastSeenBy(Myself),FLAMING_FIST)
      !HPLT(Myself,60)
THEN
	RESPONSE #20
            ForceSpell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
	RESPONSE #20
            ForceSpell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
      RESPONSE #20
            ForceSpell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
	RESPONSE #20
		ApplySpell(LastSeenBy(Myself), WIZARD_DISPEL_MAGIC)
      RESPONSE #20
            ForceSpell(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
END

IF
      !Global("BW05_GraTalkedToTergius","GLOBAL",3) 
      See([ENEMY.0.0])
	CheckStatGT(LastSeenBy(Myself), 55, RESISTMAGIC)
      !Allegiance(Myself, ENEMY)
      !Class(LastSeenBy(Myself),INNOCENT)
	!Class(LastSeenBy(Myself),FLAMING_FIST)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ApplySpell(LastSeenBy(Myself), WIZARD_LOWER_RESISTANCE)
            ForceSpell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
	StateCheck(Myself,STATE_SILENCED)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_VOCALIZE)
END

IF
	See(NearestEnemyOf(Myself))
	Global("BW05_PCEnemyTergius","GLOBAL",0)
THEN
	RESPONSE #100
	      SetGlobal("BW05_PCEnemyTergius","GLOBAL",1)
            DisplayString(Myself,@20011)
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_GREATER_MALISON)
            DisplayString(Myself,@20010)
	      ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)		
            ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
            ApplySpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
            ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
            ApplySpell(Myself,WIZARD_GHOST_ARMOR)
            ApplySpell(Myself,WIZARD_ABSOLUTE_IMMUNITY)
            ApplySpell(Myself,WIZARD_FIRE_SHIELD_RED)
            ApplySpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
            ApplySpell(Myself,CLERIC_CONJURE_EARTH_ELEMENTAL)
            DisplayString(Myself,@20015)
END

IF
	See(Player1)
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      Global("BW05_PCTergiusMS1","GLOBAL",0)
THEN
	RESPONSE #100
	      ApplySpell(Player1,WIZARD_HOLD_MONSTER)
	      SetGlobal("BW05_PCTergiusMS1","GLOBAL",1)
END

IF
	See(Player2)
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      Global("BW05_PCTergiusMS2","GLOBAL",0)
THEN
	RESPONSE #100
	      ApplySpell(Player2,WIZARD_HOLD_MONSTER)
	      SetGlobal("BW05_PCTergiusMS2","GLOBAL",1)
END

IF
	See(Player3)
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      Global("BW05_PCTergiusMS3","GLOBAL",0)
THEN
	RESPONSE #100
	      ApplySpell(Player3,WIZARD_HOLD_MONSTER)
	      SetGlobal("BW05_PCTergiusMS3","GLOBAL",1)
END

IF
	See(Player4)
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      Global("BW05_PCTergiusMS4","GLOBAL",0)
THEN
	RESPONSE #100
	      ApplySpell(Player4,WIZARD_HOLD_MONSTER)
	      SetGlobal("BW05_PCTergiusMS4","GLOBAL",1)
END

IF
	See(Player5)
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      Global("BW05_PCTergiusMS5","GLOBAL",0)
THEN
	RESPONSE #100
	      ApplySpell(Player5,WIZARD_HOLD_MONSTER)
	      SetGlobal("BW05_PCTergiusMS5","GLOBAL",1)
END

IF
	See(Player6)
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      Global("BW05_PCTergiusMS6","GLOBAL",0)
THEN
	RESPONSE #100
	      ApplySpell(Player6,WIZARD_HOLD_MONSTER)
	      SetGlobal("BW05_PCTergiusMS6","GLOBAL",1)
END

IF
	See(NearestEnemyOf(Myself))
      Global("BW05_PCEnemyTergius","GLOBAL",1)
      !Global("BW05_TGC1","GLOBAL",16)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
	      ForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
	      SetGlobal("BW05_PCEnemyTergius","GLOBAL",2)
END

IF
	See(NearestEnemyOf(Myself))
	Global("BW05_PCEnemyTergius","GLOBAL",2)
      !Global("BW05_TGC1","GLOBAL",16)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
	      ForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
	      SetGlobal("BW05_PCEnemyTergius","GLOBAL",3)
END

IF
	See(NearestEnemyOf(Myself))
	Global("BW05_PCEnemyTergSP","GLOBAL",0)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		SetGlobal("BW05_PCEnemyTergSP","GLOBAL",1)
          	SetGlobal("BW05_PCEnemyTergius","GLOBAL",4)
            ForceSpell(NearestEnemyOf(Myself),WIZARD_FLESH_TO_STONE)
END

IF
	See(NearestEnemyOf(Myself))
	Global("BW05_PCEnemyTergius","GLOBAL",4)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		SetGlobal("BW05_PCEnemyTergius","GLOBAL",5)
END

IF
	See(NearestEnemyOf(Myself))
	GlobalGT("BW05_PCEnemyTergius","GLOBAL",4)
      GlobalLT("BW05_PCEnemyTergius","GLOBAL",9)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_FLAME_ARROW)
            IncrementGlobal("BW05_PCEnemyTergius","GLOBAL",1)
END

IF
	See(NearestEnemyOf(Myself))
	Global("BW05_PCEnemyTergius","GLOBAL",9)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DISPEL_MAGIC)
		SetGlobal("BW05_PCEnemyTergius","GLOBAL",10)
END

IF
	See(NearestEnemyOf(Myself))
	Global("BW05_PCEnemyTergius","GLOBAL",10)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		SetGlobal("BW05_PCEnemyTergius","GLOBAL",11)
END

IF
	See(NearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),56, RESISTMAGIC)
      Global("BW05_PCEnemyTergius","GLOBAL",11)
      !HPLT(Myself,60)
THEN
	RESPONSE #20
            ForceSpell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
      RESPONSE #20
            ForceSpell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
      RESPONSE #20
		ForceSpell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
	RESPONSE #20
            ApplySpell(LastSeenBy(Myself), WIZARD_DISPEL_MAGIC)
      RESPONSE #20
		ForceSpell(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)

END

IF
	See(NearestEnemyOf(Myself))
	CheckStatGT(LastSeenBy(Myself), 55, RESISTMAGIC)
      Global("BW05_PCEnemyTergius","GLOBAL",11)
      !HPLT(Myself,60)
THEN
	RESPONSE #100
		ApplySpell(LastSeenBy(Myself), WIZARD_LOWER_RESISTANCE)
            ForceSpell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
	AttackedBy([ANYONE],Default)
      !Allegiance(LastAttackerOf(Myself), PC)
      !HPLT(Myself, 60)
THEN
	RESPONSE #100
		ApplySpell(LastAttackerOf(Myself), WIZARD_DISPEL_MAGIC)
            ForceSpell(LastAttackerOf(Myself),WIZARD_FLAME_ARROW)
END